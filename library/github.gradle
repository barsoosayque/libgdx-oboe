publishing {
    publications {
        gpr(MavenPublication) {
            groupId = publishedGroupId
            artifactId = artifact
            version = libraryVersion
            artifact "$buildDir/outputs/aar/${project.name}-release.aar"

            pom {
                name = libraryName
                description = libraryDescription
                url = siteUrl
                licenses {
                    license {
                        name = licenseName
                        url = licenseUrl
                    }
                }
                developers {
                    developer {
                        id = developerId
                        name = developerName
                        email = developerEmail
                    }
                }
                scm {
                    url = gitUrl
                }
            }
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/barsoosayque/libgdx-oboe") // Github Package
            credentials {
                Properties properties = new Properties()
                File propsFile = project.rootProject.file('local.properties')
                if (propsFile.exists())
                    properties.load(propsFile.newDataInputStream())

                username = properties.get("gpr.usr") as String ?: System.getenv("GPR_USER")
                password = properties.get("gpr.key") as String ?: System.getenv("GPR_API_KEY")
            }
        }
    }
}